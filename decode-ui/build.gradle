plugins {
	id 'application'
	id 'edu.sc.seis.launch4j' version '2.5.3'
	id 'com.github.johnrengelman.shadow' version '7.1.2'
	id 'org.beryx.runtime' version '1.12.7'
}

dependencies {
	implementation(project(':decode-core'))

	implementation 'com.formdev:flatlaf:2.4'
	implementation 'com.formdev:flatlaf-extras:2.4'
	implementation 'com.miglayout:miglayout-swing:5.3'
	// https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
	implementation 'org.apache.commons:commons-lang3:3.12.0'
	// https://mvnrepository.com/artifact/org.ini4j/ini4j
	implementation 'org.ini4j:ini4j:0.5.4'
	// https://mvnrepository.com/artifact/commons-io/commons-io
	implementation 'commons-io:commons-io:2.11.0'
	implementation 'net.lingala.zip4j:zip4j:2.11.1'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

application {
	applicationName = 'decode-ui'
	mainClass.set('top.yinlingfeng.xlog.decode.ui.XLogDecodeUi')
	// The option -XX:+UseG1GC is only relevant for Java 8. Starting with Java 9 G1GC is already the default GC
	applicationDefaultJvmArgs = ['-Xms128M', '-XX:MaxRAMPercentage=70.0', '-XX:+UseG1GC',
								 '-Dawt.useSystemAAFontSettings=lcd', '-Dswing.aatext=true',
								 '-Djava.util.Arrays.useLegacyMergeSort=true']
}

applicationDistribution.with {
	into('') {
		from '../'
		include 'README.md'
		include 'NOTICE'
		include 'LICENSE'
	}
}

jar {
	manifest {
		if( JavaVersion.current() >= JavaVersion.VERSION_1_9 ) {
			attributes(
					"Main-Class": application.mainClass.get(),
					"Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' '),
					"Multi-Release": true
			)
		} else {
			attributes(
					"Main-Class": application.mainClass.get(),
					"Class-Path": configurations.runtimeClasspath.collect { it.getName() }.join(' ')
			)
		}
	}
	exclude( "module-info.class" )
	exclude( "META-INF/versions/*/module-info.class" )
}

shadow {
	mainClassName = application.mainClass.get()
}
shadowJar {
	mergeServiceFiles()
}

startScripts {
	doLast {
		def str = windowsScript.text
		str = str.replaceAll('java.exe', 'javaw.exe')
		str = str.replaceAll('"%JAVA_EXE%" %DEFAULT_JVM_OPTS%', 'start "decode-ui" /B "%JAVA_EXE%" %DEFAULT_JVM_OPTS%')
		windowsScript.text = str
	}
}

launch4j {
	mainClassName = application.mainClass.get()
	copyConfigurable = project.tasks.shadowJar.outputs.files
	jarTask = project.tasks.shadowJar
	icon = "${projectDir}/src/main/resources/logo/logo.ico"
	outfile = "decode-ui-${version}.exe"
	copyright = 'Yin'
	windowTitle = 'YXLogDecode'
	companyName = 'YXLogDecode'
	jreMinVersion = '11'
	jvmOptions = ['-Dawt.useSystemAAFontSettings=lcd', '-Dswing.aatext=true', '-XX:+UseG1GC', '-Djava.util.Arrays.useLegacyMergeSort=true', '-Dfile.encoding=utf-8']
	jreRuntimeBits = "64"
	bundledJre64Bit = true
	initialHeapPercent = 5
	maxHeapSize = 1024
	maxHeapPercent = 70
	downloadUrl = 'https://www.oracle.com/java/technologies/downloads/#jdk17-windows'
	bundledJrePath = '%EXEDIR%/jre'
}

runtime {
	addOptions('--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages')
	addModules(
			'java.base',
			'java.desktop',
			'java.logging'
	)
	jpackage {
		imageOptions = ['--icon', "${projectDir}/src/main/resources/logo/logo.ico"]
		skipInstaller = true
		targetPlatformName = "win"
	}
	launcher {
		noConsole = true
	}
}

task copyDistWinWithJre(type: Copy, dependsOn: ['runtime', 'createExe']) {
	group 'YXLogDecode'
	destinationDir = new File(buildDir, "decode-ui-${rootProject.version}-with-jre-win")
	from(runtime.jreDir) {
		include '**/*'
		into 'jre'
	}
	from(createExe.outputs) {
		include '*.exe'
	}
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task distWinWithJre(type: Zip, dependsOn: ['copyDistWinWithJre']) {
	group 'YXLogDecode'
	destinationDirectory = buildDir
	archiveFileName = "decode-ui-${rootProject.version}-with-jre-win.zip"
	from(copyDistWinWithJre.outputs) {
		include '**/*'
	}
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
